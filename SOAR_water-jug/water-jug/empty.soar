# water-jug*propose*empty
# If the task is water-jug and there is a non-empty jug,
# then propose emptying that jug.

sp {water-jug*propose*empty
   (state <s> ^name water-jug
              ^jug <j>)
   (<j> ^contents > 0)
-->
   (<s> ^operator <o> +)
   (<o> ^name empty
        ^empty-jug <j>)
}

# water-jug*apply*empty
# If the task is water-jug and empty is selected for a given jug,
# then set that jug's contents to 0.

sp {water-jug*apply*empty
   (state <s> ^name water-jug
              ^operator <o>
              ^jug <j>)
   (<o> ^name empty
        ^empty-jug <j>)
   (<j> ^contents <c>)
-->
   (<j> ^contents 0
        ^contents <c> -)}

# water-jug*apply*operator*record*last-operator*empty
# If wj and last is empty create augmentation with last operator and its augmentations.

sp {water-jug*apply*operator*record*last-operator*empty
   (state <s> ^name water-jug
              ^operator <o>)
   (<o> ^name empty
        ^empty-jug <eJug>)
-->
   (<s> ^lastOperator <lastOp>)
   (<lastOp> ^name empty
             ^empty-jug <eJug>)}

# water-jug*apply*operator*remove*old*last-operator*empty

sp {water-jug*apply*operator*remove*last-operator*empty
   (state <s> ^name water-jug
              ^operator <o>
              ^lastOperator <lastOp>)
   (<o> ^name empty
        ^empty-jug <eJug>)
  -(<lastOp> ^name empty
             ^empty-jug <eJug>)
-->
   (<s> ^lastOperator <lastOp> -)}
