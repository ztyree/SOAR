# water-jug*propose*fill
# If the task is water-jug and there is a jug that is not full,
# then propose filling that jug.

sp {water-jug*propose*fill
   (state <s> ^name water-jug
              ^jug <j>)
   (<j> ^empty > 0)
-->
   (<s> ^operator <op> + =)
   (<op> ^name fill
         ^fill-jug <j>)
}

# water-jug*apply*fill
# If the task is water-jug and the fill operator is selected for a given jug,
# then set that jug's contents to be its volume.

sp {water-jug*apply*fill
   (state <s> ^name water-jug
              ^operator <o>
              ^jug <j>)
   (<o> ^name fill
        ^fill-jug <j>)
   (<j> ^volume <v>
        ^contents <c>)
-->
   (<j> ^contents <v>)
   (<j> ^contents <c> -)}

# water-jug*apply*operator*record*last-operator*fill
# If wj and last is fill create augmentation with last operator and its augmentations.

sp {water-jug*apply*operator*record*last-operator*fill
   (state <s> ^name water-jug
              ^operator <o>)
   (<o> ^name fill
        ^fill-jug <fJug>)
-->
   (<s> ^lastOperator <lastOp>)
   (<lastOp> ^name fill
             ^fill-jug <fJug>)}

# water-jug*apply*operator*remove*old*last-operator*empty

sp {water-jug*apply*operator*remove*last-operator*fill
   (state <s> ^name water-jug
              ^operator <o>
              ^lastOperator <lastOp>)
   (<o> ^name fill
        ^fill-jug <fJug>)
  -(<lastOp> ^name fill
             ^fill-jug <fJug>)
-->
   (<s> ^lastOperator <lastOp> -)}