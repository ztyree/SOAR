sp {MISS_CAN*detectFinished
   (state <s> ^desired <d>
              ^<side> <sd>)
   (<sd> ^missionaries <m>
        ^cannibals <c>)
   (<d> ^{ << rightBank leftBank >> <side> } <dSide>)
   (<dSide> ^missionaries <m>
            ^cannibals <c>)
-->
   (<s> ^success <d>)
   (write (crlf) |Success!|)}

sp {MISS_CAN*detectFailure
   (state <s> ^desired <d>
              ^<< rightBank leftBank >> <b>)
   (<b> ^missionaries { <m> > 0 }
        ^cannibals > <m>)
-->
   (<s> ^failure <d>)
   (write (crlf) |Failure!|)}

sp {MISS_CAN*select*inverse*failure*types1
   (state <s> ^name MISS_CAN
              ^operator <o> +
              ^failure 1
              ^lastOperator <lo>)
   (<o> ^name movePerson
        ^<type> <number>
        ^types 1)
   (<lo> ^types 1
         ^type <type>
         ^number <number>)
-->
   (<s> ^operator <o> >)}
              
sp {MISS_CAN*select*inverse*failure*types2
   (state <s> ^name MISS_CAN
              ^operator <o> +
              ^failure 1
              ^lastOperator.types 2)
   (<o> ^types 2)
-->
   (<s> ^operator <o> >)}


sp {MISS_CAN*avoid*inverse*notFailure*types1
   (state <s> ^name MISS_CAN
              ^operator <o> +
             -^failure true
              ^lastOperator <lo>)
   (<o> ^types 1
        ^<type> <number>)
   (<lo> ^types 1
         ^type <type>
         ^number <number>)
-->
   (<s> ^operator <o> < )}

sp {MISS_CAN*avoid*inverse*notFailure*types2
   (state <s> ^name MISS_CAN
              ^operator <o> +
             -^failure true
              ^lastOperator.types 2)
   (<o> ^types 2)
-->
   (<s> ^operator <o> < )}

sp {MISS_CAN*evaluate*state*failure*duplicate
   (state <s1> ^superstate.superstate <s2>
               ^desired <d>
               ^rightBank <rb>
               ^leftBank <lb>)
   (<rb> ^missionaries <rbm> ^cannibals <rbc> ^boat <rbb>)
   (<lb> ^missionaries <lbm> ^cannibals <lbc> ^boat <lbb>)
   (<s2> ^rightBank <rb2>
         ^leftBank <lb2>)
   (<rb2> ^missionaries <rbm> ^cannibals <rbc> ^boat <rbb>)
   (<lb2> ^missionaries <lbm> ^cannibals <lbc> ^boat <lbb>)
-->
   (write (crlf) |Duplicate State Detected.|)
   (<s2> ^failure <d>)}
   