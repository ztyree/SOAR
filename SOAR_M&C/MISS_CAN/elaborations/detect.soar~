sp {MISS_CAN*detectFinished
   (state <s> ^name MISS_CAN
              ^desired <d>
              ^<side> <sd>)
   (<sd> ^missionaries <m>
        ^cannibals <c>
        ^boat <b>)
   (<d> ^{ << rightBank leftBank >> <side> } <dSide>)
   (<dSide> ^missionaries <m>
            ^cannibals <c>
            ^boat <b>)
-->
   (write (crlf) |The problem has been solved.|)
   (halt)}

sp {MISS_CAN*detectFailure
   (state <s> ^name MISS_CAN
              ^<< rightBank leftBank >> <b>)
   (<b> ^missionaries { <m> > 0 }
        ^cannibals > <m>)
-->
   (<s> ^failure 1)
   (write (crlf) |FAILURE, UNDOING LAST MOVE|)}

sp {MISS_CAN*select*inverse*failure*types1
   (state <s> ^name MISS_CAN
              ^operator <o> +
              ^failure 1
              ^lastOperator <lo>)
   (<o> ^name movePerson
        ^<type> <number>
        ^types 1)
   (<lo> ^types 1
         ^type <type>
         ^number <number>)
-->
   (<s> ^operator <o> >)}
              
sp {MISS_CAN*select*inverse*failure*types2
   (state <s> ^name MISS_CAN
              ^operator <o> +
              ^failure 1
              ^lastOperator.types 2)
   (<o> ^types 2)
-->
   (<s> ^operator <o> >)}


sp {MISS_CAN*avoid*inverse*notFailure*types1
   (state <s> ^name MISS_CAN
              ^operator <o> +
             -^failure true
              ^lastOperator <lo>)
   (<o> ^types 1
        ^<type> <number>)
   (<lo> ^types 1
         ^type <type>
         ^number <number>)
-->
   (<s> ^operator <o> < )}

sp {MISS_CAN*avoid*inverse*notFailure*types2
   (state <s> ^name MISS_CAN
              ^operator <o> +
             -^failure true
              ^lastOperator.types 2)
   (<o> ^types 2)
-->
   (<s> ^operator <o> < )}
   