
sp {MISS_CAN*propose*operator*moveBoat1m
   "Moves a single missionary."
   (state <s> ^name MISS_CAN
              ^boat <bank>
              ^<bank>.missionaries > 0)
-->
   (<s> ^operator <o> +)
   (<o> ^name moveBoat
        ^missionaries 1
        ^cannibals 0)}


sp {MISS_CAN*propose*operator*moveBoat1c
   "Moves a single cannibal."
   (state <s> ^name MISS_CAN
              ^boat <bank>
              ^<bank>.cannibals > 0)
-->
   (<s> ^operator <o> +)
   (<o> ^name moveBoat
        ^cannibals 1
        ^missionaries 0)}


sp {MISS_CAN*propose*operator*moveBoat2m
   "Moves two missionaries."
   (state <s> ^name MISS_CAN
              ^boat <bank>
              ^<bank>.missionaries > 1)
-->
   (<s> ^operator <o> +)
   (<o> ^name moveBoat
        ^missionaries 2
        ^cannibals 0)}


sp {MISS_CAN*propose*operator*moveBoat2c
   "Moves two cannibals."
   (state <s> ^name MISS_CAN
              ^boat <bank>
              ^<bank>.cannibals > 1)
-->
   (<s> ^operator <o> +)
   (<o> ^name moveBoat
        ^cannibals 2
        ^missionaries 0)}

sp {MISS_CAN*propose*operator*moveBoat11
   (state <s> ^name MISS_CAN
              ^boat <bank>
              ^<bank> <b>)
   (<b> ^missionaries > 0
        ^cannibals > 0)
-->
   (<s> ^operator <o> +)
   (<o> ^name moveBoat
        ^missionaries 1
        ^cannibals 1)}

###
### moveBoat OPERATOR IMPLEMENTATION
###
#
sp {apply*moveBoat*not-1missionary-1-cannibal
   (state <s> ^operator <o>                        
              ^boat <bank>
              ^oBank <oBank>
              ^<bank> <b> 
              ^<oBank> <ob>)
   (<b> ^<type> <bank-num>)
   (<ob> ^<type> <oBank-num>)
   (<o> ^name moveBoat
        ^{ << missionaries cannibals >> <type> } { <> 0 <num> }
        ^{ << missionaries cannibals >> <o-type> } 0)
-->
   (<b> ^<type> <bank-num> -
               (- <bank-num> <num>))
   (<ob> ^<type> <oBank-num> -
               (+ <oBank-num> <num>))
   (<s> ^boat <bank> -
              <oBank>
        ^oBank <oBank> -
                <bank>)
}

sp {apply*moveBoat*1-missionary-1-cannibal*right
   (state <s> ^operator <o>                        
              ^boat rightBank
              ^oBank leftBank
              ^rightBank <b> 
              ^leftBank <ob>)
   (<b> ^missionaries <m-num>
        ^cannibals <c-num>)
   (<ob> ^missionaries <o-m-num>
         ^cannibals <o-c-num>)
   (<o> ^name moveBoat
        ^missionaries 1
        ^cannibals 1)
-->
   (<b> ^missionaries <m-num> -
                      (- <m-num> 1)
        ^cannibals <c-num> -
                      (- <c-num> 1))
   (<ob> ^missionaries <o-m-num> -
                      (+ <o-m-num> 1)
         ^cannibals <o-c-num> -
                      (+ <o-c-num> 1))
   (<s> ^boat rightBank -
              leftBank
        ^oBank leftBank -
                rightBank)
}

sp {apply*moveBoat*1-missionary-1-cannibal*left
   (state <s> ^operator <o>                        
              ^boat  leftBank
              ^oBank rightBank
              ^leftBank <b> 
              ^rightBank <ob>)
   (<b> ^missionaries <m-num>
        ^cannibals <c-num>)
   (<ob> ^missionaries <o-m-num>
         ^cannibals <o-c-num>)
   (<o> ^name moveBoat
        ^missionaries 1
        ^cannibals 1)
-->
   (<b> ^missionaries <m-num> -
                      (- <m-num> 1)
        ^cannibals <c-num> -
                      (- <c-num> 1))
   (<ob> ^missionaries <o-m-num> -
                      (+ <o-m-num> 1)
         ^cannibals <o-c-num> -
                      (+ <o-c-num> 1))
   (<s> ^boat leftBank -
               rightBank 
        ^oBank rightBank -
                leftBank)
}